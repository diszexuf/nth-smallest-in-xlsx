plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.7'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.openapi.generator' version '7.15.0'
}

group = 'ru.diszexuf'
version = '0.0.1-SNAPSHOT'
description = 'minimum-number'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    implementation 'org.apache.poi:poi-ooxml:5.4.1'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.13'
    implementation 'org.springframework.boot:spring-boot-starter-validation:3.5.6'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.8'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform()
}

openApiGenerate {
    generatorName = 'spring'
    inputSpec = file('src/main/resources/openapi/openapi.yaml').absolutePath
    outputDir = file('build/generated').absolutePath

    apiPackage = 'ru.diszexuf.minimumnumber.api'
    modelPackage = 'ru.diszexuf.minimumnumber.model'
    invokerPackage = 'ru.diszexuf.minimumnumber.invoker'

    configOptions = [
            interfaceOnly         : 'true',
            useOptional           : 'false',
            skipDefaultInterface  : 'true',
            useTags               : 'true',
            useSpringValidation   : 'true',
            useBeanValidation     : 'true',
            library               : 'spring-boot',
            useJakartaEe          : 'true',
            useNullable           : 'false',
            serializableModel     : 'true',
            dateLibrary           : 'java8'
    ]

    globalProperties = [
            apis            : '',
            models          : '',
            supportingFiles : 'false'
    ]
}

tasks.register('cleanOpenApiGenerate', Delete) {
    delete file('build/generated')
}

tasks.named('openApiGenerate') {
    dependsOn cleanOpenApiGenerate
}

tasks.named('compileJava').configure {
    dependsOn 'openApiGenerate'
}

sourceSets {
    main {
        java {
            srcDir('build/generated/src/main/java')
        }
    }
}